# This policy validates that all modules loaded directly by the
# root module are in the Private Module Registry (PMR) of a TFC
# server and that no resources are created in the root module

#####Imports#####
import "tfconfig"
import "strings"

##### Global Variables #####
# Define the address of the TFE server
address = "app.terraform.io"

# Define organization variable
organization = "Cloud-Operations"

##### Rules #####
# Don't allow resources in root module
no_resources_in_root_module = rule {
   length(tfconfig.resources) is 0 or not
   (print("Resources not allowed in root module") and
    print("Your root module has", length(tfconfig.resources), "resources."))

}

# Require all modules directly under root module to come from PMR
require_modules_from_pmr = rule {
  all tfconfig.modules as name, m {
    strings.has_prefix(m.source, address + "/" + organization) or not
    (print("All modules must from the private module registry",
    address + "/" + organization) and
   print("You included module", name, "with source", m.source))
  }
}

# Main rule that requires other rules to be true
main = rule {
  print("modules", tfconfig.modules) and
  no_resources_in_root_module and
  require_modules_from_pmr
}
